plugins {

    id 'org.jetbrains.kotlin.jvm' version '2.2.21' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21' apply false
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.2.21' apply false
    id 'org.springframework.boot' version '4.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.google.devtools.ksp' version '2.2.21-2.0.4' apply false
}

allprojects {
    group = 'com.nativeflow'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
    apply plugin: 'com.google.devtools.ksp'
    apply plugin: 'kotlin-kapt'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'com.github.f4b6a3:uuid-creator:6.1.1'
        implementation 'org.mapstruct:mapstruct:1.6.3'
        kapt 'org.mapstruct:mapstruct-processor:1.6.3'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }

    kotlin {
        compilerOptions {
            freeCompilerArgs.addAll '-Xjsr305=strict'
        }
    }

    kapt {
        arguments {
            arg("mapstruct.defaultComponentModel", "spring")
            arg("mapstruct.unmappedTargetPolicy", "IGNORE")
        }

        useBuildCache = true
    }

    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
}

project(':') {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        implementation project(':identity')
        implementation project(':content')
        implementation project(':gamification')
        implementation project(':learning')
        implementation project(':common')

        implementation 'jakarta.persistence:jakarta.persistence-api'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    }

    bootJar {
        enabled = true
    }
    jar {
        enabled = false
    }
}